##
# TCSH Expanded C-Shell INITIALIZATION FILE
# Aliases file
#
# Wilfredo Sanchez Jr. | tritan@mit.edu
# June 10, 1994
#
# MIT Project Athena
##
alias .		'pwd'
alias ..	'cd ..'
alias cd..	'cd ..'
alias cdwd	'cd `pwd`'
alias cwd	'echo $cwd'
alias files	'find \!:1 -type f -print'	# files x => list files in x
alias ff	'find . -name \!:1 -print'	# ff x => find file named x
alias line	'sed -n '\''\!:1 p'\'' \!:2'	# line 5 file => show line 5 of file
alias l 	'ls -lg'
alias ll	'ls -lag \!* | more'
alias term 	'set noglob; unsetenv TERMCAP; eval `tset -s -I -Q - \!*`'
alias word	'grep \!* /usr/share/dict/web2'	# Grep thru dictionary
alias wordcount	'(cat \!* | tr -s '\'' 	.,;:?\!()[]"'\'' '\''\012'\'' |' \
		'cat -n | tail -1 | awk '\''{print $1}'\'')' # Histogram words

##
# Read user's aliases
##
if (-r ${tcsh_initdir}/aliases.mine) then
  source ${tcsh_initdir}/aliases.mine
endif
